# $Id: PKGBUILD 159787 2016-02-01 13:20:44Z arodseth $
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Robert Hollencamp <rhollencamp@gmail.com>
# Contributor: Daniel J Griffiths <griffithsdj@inbox.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=codeblocks
pkgver=16.01
pkgrel=1
pkgdesc='Cross-platform C/C++ IDE'
arch=('x86_64' 'i686')
url='http://www.codeblocks.org/'
license=('GPL3')
depends=('boost-libs' 'desktop-file-utils' 'gamin' 'gtk-update-icon-cache'
         'hicolor-icon-theme' 'hunspell' 'shared-mime-info' 'wxgtk2.8' 'libsm' 'webkitgtk2')
makedepends=('boost' 'zip' 'webkitgtk2')
install=codeblocks.install
source=("http://sourceforge.net/projects/codeblocks/files/Sources/$pkgver/codeblocks_$pkgver.tar.gz")
sha256sums=('30fb9d206df4ab3c2e2fec2451a0a7c9723bfed9a673cc9be71e881c529d5a03')

build() {
  cd "$pkgname-$pkgver.release"

  # Fix build with boost 1.59
  export CPPFLAGS+=" -DBOOST_ERROR_CODE_HEADER_ONLY"
  export WX_CONFIG_PATH=/usr/bin/wx-config-2.8

  ./bootstrap
  ./configure --prefix=/usr --with-contrib-plugins=all
  make
}

package() {
  make -C "$pkgname-$pkgver.release" DESTDIR="$pkgdir" install
}

# getver: -u 2 codeblocks.org/downloads/source
# vim:set ts=2 sw=2 et:
